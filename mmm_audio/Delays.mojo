from .MMMWorld_Module import *
from .functions import *
from math import tanh
from .Filters import *
from math import log
from .Recorder_Module import Recorder
from bit import next_power_of_two

struct Delay[num_chans: Int = 1, interp: Int = Interp.linear](Representable, Movable, Copyable):
    """A variable delay line with interpolation.

    Parameters:
      num_chans: Size of the SIMD vector - defaults to 1.
      interp: The interpolation method to use. See the struct [Interp](MMMWorld.md#struct-interp) for interpolation options.
    """

    var world: UnsafePointer[MMMWorld]
    var max_delay_time: Float64
    var max_delay_samples: Int64
    var delay_line: Recorder[num_chans]
    var two_sample_duration: Float64
    var sample_duration: Float64
    var prev_f_idx: List[Float64]

    fn __init__(out self, world: UnsafePointer[MMMWorld], max_delay_time: Float64 = 1.0):
      """Initialize the Delay line.

      Args:
        world: A pointer to the MMMWorld.
        max_delay_time: The maximum delay time in seconds. The internal buffer will be allocated to accommodate this delay.
      """
        self.world = world
        self.max_delay_time = max_delay_time
        self.max_delay_samples = Int64(max_delay_time * self.world[].sample_rate)
        self.delay_line = Recorder[num_chans](self.world, self.max_delay_samples, self.world[].sample_rate)
        self.two_sample_duration = 2.0 / self.world[].sample_rate
        self.sample_duration = 1.0 / self.world[].sample_rate
        self.prev_f_idx = List[Float64](self.num_chans, 0.0)

    fn __repr__(self) -> String:
        return String("Delay(max_delay_time: " + String(self.max_delay_time) + ")")

    @always_inline
    fn read(mut self, var delay_time: SIMD[DType.float64, self.num_chans]) -> SIMD[DType.float64, self.num_chans]:
      """Reads into the delay line.

      Args:
        delay_time: The amount of delay to apply (in seconds).

      Returns:
        A single sample read from the delay buffer.
      """
      delay_time = min(delay_time, self.max_delay_time)
      # print(delay_time)
        
      out = SIMD[DType.float64, self.num_chans](0.0)
      # minimum delay time depends on interpolation method

      @parameter
      for chan in range(self.num_chans):
        @parameter
        if self.interp == Interp.none:
          delay_time = max(delay_time, 0.0)
          out[chan] = ListInterpolator.read_none[bWrap=True](self.delay_line.buf.data[chan], self.get_f_idx(delay_time[chan]))
        elif self.interp == Interp.linear:
          delay_time = max(delay_time, 0.0)
          out[chan] = ListInterpolator.read_linear[bWrap=True](self.delay_line.buf.data[chan], self.get_f_idx(delay_time[chan]))
        elif self.interp == Interp.quad:
          delay_time = max(delay_time, 0.0)
          out[chan] = ListInterpolator.read_quad[bWrap=True](self.delay_line.buf.data[chan], self.get_f_idx(delay_time[chan]))
        elif self.interp == Interp.cubic:
          delay_time = max(delay_time, self.sample_duration)
          out[chan] = ListInterpolator.read_cubic[bWrap=True](self.delay_line.buf.data[chan], self.get_f_idx(delay_time[chan]))
        elif self.interp == Interp.lagrange4:
          delay_time = max(delay_time, 0.0)
          out[chan] = ListInterpolator.read_lagrange4[bWrap=True](self.delay_line.buf.data[chan], self.get_f_idx(delay_time[chan]))
        elif self.interp == Interp.sinc:
          # [TODO] How many minimum samples do we need for sinc interpolation?
          delay_time = max(delay_time, 0.0)
          f_idx = self.get_f_idx(delay_time[chan])
          out[chan] = ListInterpolator.read_sinc[bWrap=True](self.world, self.delay_line.buf.data[chan], self.get_f_idx(delay_time[chan]), self.prev_f_idx[chan])
          self.prev_f_idx[chan] = f_idx
          
      return out

    @always_inline
    fn write(mut self, input: SIMD[DType.float64, self.num_chans]):
      """Writes a single sampleinto the delay line."""

        self.delay_line.write_previous(input)

    @always_inline
    fn next(mut self, input: SIMD[DType.float64, self.num_chans], var delay_time: SIMD[DType.float64, self.num_chans]) -> SIMD[DType.float64, self.num_chans]:
        """Process one sample through the delay line, first reading from the delay then writing into it.

        next(input, delay_time)
        
        Args:
          input: The input sample to process.
          delay_time: The amount of delay to apply (in seconds).

        Returns:
          The processed output sample.

        """
        
        out = self.read(delay_time)
        self.write(input)

        return out

    @always_inline
    fn get_f_idx(self, delay_time: Float64) -> Float64:
        """Calculate the fractional index in the delay buffer for the given delay time.

        Args:
          delay_time: The delay time in seconds.

        Returns:
          The fractional index in the delay buffer.
        """

        delay_samps = delay_time * self.world[].sample_rate
        # Because the ListInterpolator functions always "read" forward,
        # we're writing into the delay line buffer backwards, so therefore,
        # here to go backwards in time we add the delay samples to the write head.
        f_idx = (Float64(self.delay_line.write_head) + delay_samps + 1) % Float64(self.delay_line.buf.num_frames)
        return f_idx

fn calc_feedback[num_chans: Int = 1](delaytime: SIMD[DType.float64, num_chans], decaytime: SIMD[DType.float64, num_chans]) -> SIMD[DType.float64, num_chans]:
      """Calculate the feedback coefficient for a Comb filter or Allpass line based on desired delay time and decay time.
      
      Parameters:
        num_chans: Size of the SIMD vector - defaults to 1.

      Args:
        delaytime: The delay time in seconds.
        decaytime: The decay time in seconds (time to -60dB)."""
      
      alias log001: Float64 = log(0.001)

      zero: SIMD[DType.bool, num_chans] = delaytime.eq(0) or decaytime.eq(0)
      dec_pos: SIMD[DType.bool, num_chans] = decaytime.ge(0)

      absret = exp(log001 * delaytime / abs(decaytime))

      return zero.select(SIMD[DType.float64, num_chans](0.0), dec_pos.select(absret, -absret))

struct Comb[num_chans: Int = 1, interp: Int = 2](Movable, Copyable):
    """
    A simple comb filter using a delay line with feedback.

    Parameters:
      num_chans: Size of the SIMD vector - defaults to 1.
      interp: The interpolation method to use. See the struct [Interp](MMMWorld.md#struct-interp) for interpolation options.

    """

    var world: UnsafePointer[MMMWorld]
    var delay: Delay[num_chans, interp]
    var fb: SIMD[DType.float64, num_chans]

    fn __init__(out self, world: UnsafePointer[MMMWorld], max_delay: Float64 = 1.0):
      """Initialize the Comb filter.

      Args:
        world: A pointer to the MMMWorld.
        max_delay: The maximum delay time in seconds. The internal buffer will be allocated to accommodate this delay.
      """
        self.world = world
        self.delay = Delay[num_chans, interp](self.world, max_delay)
        self.fb = SIMD[DType.float64, num_chans](0.0)

    fn next(mut self, input: SIMD[DType.float64, self.num_chans], delay_time: SIMD[DType.float64, self.num_chans] = 0.0, feedback: SIMD[DType.float64, self.num_chans] = 0.0) -> SIMD[DType.float64, self.num_chans]:
        """Process one sample through the comb filter.
        
        Args:
          input: The input sample to process.
          delay_time: The amount of delay to apply (in seconds).
          feedback: The amount of feedback to apply (0.0 to 1.0).

        Returns:
          The delayed output sample.
        """
        # Get the delayed sample
        # does not write to the buffer
        var out = self.delay.next(self.fb, delay_time)  
        temp = input + out * clip(feedback, 0.0, 1.0)  # Apply feedback

        self.fb = temp

        return out  # Return the delayed sample

    fn next_decaytime(mut self, input: SIMD[DType.float64, self.num_chans], delay_time: SIMD[DType.float64, self.num_chans], decay_time: SIMD[DType.float64, self.num_chans]) -> SIMD[DType.float64, self.num_chans]:
        """Process one sample through the comb filter with decay time calculation.
        
        Args:
          input: The input sample to process.
          delay_time: The amount of delay to apply (in seconds).
          decay_time: The desired decay time (time to -60dB). Feedback is calculated internally.

        Returns:
          The delayed output sample.
        """
        feedback = calc_feedback(delay_time, decay_time)
        return self.next(input, delay_time, feedback)

struct LP_Comb[num_chans: Int = 1, interp: Int = Interp.linear](Movable, Copyable):
    """
    A simple comb filter with an integrated virtual analog one-pole low-pass filter.
    
    Parameters:
      num_chans: Size of the SIMD vector - defaults to 1.
      interp: The interpolation method to use. See the struct [Interp](MMMWorld.md#struct-interp) for interpolation options.
    """
    var world: UnsafePointer[MMMWorld]
    var delay: Delay[num_chans, interp] # Delay line without automatic feedback
    var one_pole: VAOnePole[num_chans]
    var fb: SIMD[DType.float64, num_chans]

    fn __init__(out self, world: UnsafePointer[MMMWorld], max_delay: Float64 = 1.0):
      """Initialize the LP_Comb filter.

      Args:
        world: A pointer to the MMMWorld.
        max_delay: The maximum delay time in seconds. The internal buffer will be allocated to accommodate this delay.
      """ 

        self.world = world
        self.delay = Delay[num_chans, interp](self.world, max_delay)
        self.one_pole = VAOnePole[num_chans](self.world)
        self.fb = SIMD[DType.float64, num_chans](0.0)

    @always_inline
    fn next(mut self, input: SIMD[DType.float64, self.num_chans], delay_time: SIMD[DType.float64, self.num_chans] = 0.0, feedback: SIMD[DType.float64, self.num_chans] = 0.0, lp_freq: SIMD[DType.float64, self.num_chans] = 0.0) -> SIMD[DType.float64, self.num_chans]:
        """Process one sample through the comb filter.

        Args:
          input: The input sample to process.
          delay_time: The amount of delay to apply (in seconds).
          feedback: The amount of feedback to apply (0.0 to 1.0).
          lp_freq: The cutoff frequency of the VAOnePole filter in the feedback loop.

        Returns:
          The processed output sample.
        """
        var out = self.delay.next(self.fb, delay_time)  # Get the delayed sample

        self.fb = self.one_pole.lpf(out * clip(feedback, 0.0, 1.0), lp_freq)  # Low-pass filter the feedback

        self.fb += input

        return out  # Return the delayed sample

    fn __repr__(self) -> String:
        return "LP_Comb"

struct Allpass_Comb[num_chans: Int = 1, interp: Int = Interp.linear](Movable, Copyable):
    """
    A simple allpass comb filter using a delay line with feedback.
    
    Parameters:
      num_chans: Size of the SIMD vector - defaults to 1.
      interp: The interpolation method to use. See the struct [Interp](MMMWorld.md#struct-interp) for interpolation options.
    """
    var world: UnsafePointer[MMMWorld]
    var delay: Delay[num_chans, interp]

    fn __init__(out self, world: UnsafePointer[MMMWorld], max_delay: Float64 = 1.0):
      """Initialize the Allpass Comb filter.

      Args:
        world: A pointer to the MMMWorld.
        max_delay: The maximum delay time in seconds. The internal buffer will be allocated to accommodate this delay.
      """

        self.world = world
        self.delay = Delay[num_chans, interp](self.world, max_delay)

    fn next(mut self, input: SIMD[DType.float64, self.num_chans], delay_time: SIMD[DType.float64, self.num_chans] = 0.0, feedback_coef: SIMD[DType.float64, self.num_chans] = 0.0) -> SIMD[DType.float64, self.num_chans]:
        """Process one sample through the allpass comb filter.

        Args:
          input: The input sample to process.
          delay_time: The amount of delay to apply (in seconds).
          feedback_coef: The feedback coefficient (-1.0 to 1.0).

        Returns:
          The delayed/filtered output sample.
        """

        var delayed = self.delay.read(delay_time)
        var to_delay = input + feedback_coef * delayed
        var output = (-feedback_coef * input) + delayed
        
        self.delay.write(to_delay)
        
        return output

    fn next_decaytime(mut self, input: SIMD[DType.float64, self.num_chans], delay_time: SIMD[DType.float64, self.num_chans], decay_time: SIMD[DType.float64, self.num_chans]) -> SIMD[DType.float64, self.num_chans]:
        """Process one sample through the allpass comb filter with decay time calculation.
        
        Args:
          input: The input sample to process.
          delay_time: The amount of delay to apply (in seconds).
          decay_time: The desired decay time (time to -60dB).
        
        """
        feedback = calc_feedback(delay_time, decay_time)
        return self.next(input, delay_time, feedback)
    

struct FB_Delay[num_chans: Int = 1, interp: Int = Interp.lagrange4, ADAA_dist: Bool = False, os_index: Int = 0](Representable, Movable, Copyable):
    """A feedback delay structured like a Comb filter, but with possible feedback coefficient above 1 due to an integrated tanh function.
    
    By default, Anti-aliasing is disabled and no oversampling is applied, but this can be changed by setting the ADAA_dist and os_index template parameters.
    
    Parameters:
      num_chans: Size of the SIMD vector - defaults to 1.
      interp: The interpolation method to use. See the struct [Interp](MMMWorld.md#struct-interp) for interpolation options.
      ADAA_dist: Whether to apply ADAA distortion to the feedback signal instead of standard tanh.
      os_index: The oversampling index for ADAA distortion. 0 = no oversampling, 1 = 2x, 2 = 4x, 3 = 8x, 4 = 16x.
    """

    var world: UnsafePointer[MMMWorld]
    var delay: Delay[num_chans, interp]
    var dc: DCTrap[num_chans]
    var fb: SIMD[DType.float64, num_chans]
    var tanh_ad: TanhAD[num_chans, os_index]

    fn __init__(out self, world: UnsafePointer[MMMWorld], max_delay: Float64 = 1.0):
      """Initialize the FB_Delay.

      Args:
        world: A pointer to the MMMWorld.
        max_delay: The maximum delay time in seconds. The internal buffer will be allocated to accommodate this delay.
      """

        self.world = world
        self.delay = Delay[num_chans, interp](self.world, max_delay)
        self.dc = DCTrap[num_chans](self.world)
        self.fb = SIMD[DType.float64, num_chans](0.0)
        self.tanh_ad = TanhAD[num_chans, os_index](self.world)

    fn next(mut self, input: SIMD[DType.float64, self.num_chans], delay_time: SIMD[DType.float64, self.num_chans], feedback: SIMD[DType.float64, self.num_chans]) -> SIMD[DType.float64, self.num_chans]:
        """Process one sample or SIMD vector through the feedback delay.
        
        Args:
          input: The input sample to process.
          delay_time: The amount of delay to apply (in seconds).
          feedback: The amount of feedback to apply (0.0 to 1.0).

        Returns:
          The processed output sample or SIMD vector.
        """
        var out = self.delay.next(self.fb, delay_time)  # Get the delayed sample

        @parameter
        if ADAA_dist:
            self.fb = self.dc.next(self.tanh_ad.next((input + out) * feedback))
        else:
          self.fb = self.dc.next(tanh((input + out) * feedback))

        return out  # Return the delayed sample

    fn __repr__(self) -> String:
        return "FB_Delay"
